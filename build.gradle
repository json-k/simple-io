apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

version = '2.1.0'
group = "org.keeber"
archivesBaseName = "simple-io"

repositories { mavenCentral() }

dependencies{
	compile 'commons-net:commons-net:3.3'
	compile('jcifs:jcifs:1.3.17') {
		exclude(group: 'javax.servlet', module: 'servlet-api')
	}
	compile 'com.jcraft:jsch:0.1.53'
}

javadoc {
    failOnError = false
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
		authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
		authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
      }

      pom.project {
        name 'Simple IO'
        packaging 'jar'
        artifactId 'simple-io'
        description 'A file and folder abstraction.'
        url 'https://github.com/json-k/simple-io'

        scm {
          connection 'scm:svn:https://github.com/json-k/simple-io.git'
          developerConnection 'scm:git:https://github.com/json-k/simple-io.git'
          url 'https://github.com/json-k/simple-io'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'json-k'
            name 'json-k'
            email 'jason@keeber.org'
          }
        }
      }
    }
  }
}




